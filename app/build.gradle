plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd")
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.kotlin.mvvm"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    //使用Kotlin实验特性
//    androidExtensions {
//        experimental = true
//    }
    signingConfigs {
        release {
            storeFile file("./fhkeystore.jks")
            storePassword "fiberhome"
            keyAlias "fiberhomePension"
            keyPassword "fiberhome"
        }

        debug {
            storeFile file("./debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false //关闭混淆
            shrinkResources false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled true //开启混淆
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        disable 'InvalidPackage'
        disable "ResourceType"
        abortOnError false
    }
    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode = true
        preDexLibraries = false
        additionalParameters = [
                '--multi-dex',//多分包
                '--set-max-idx-number=60000'//每个包内方法数上限
        ]
    }
    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                //在这里修改apk文件名
                outputFileName = "Mvvm-v${variant.versionName}_${buildTime()}.apk"
            }
    }
}

dependencies {
    implementation project(path: ':helper')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //微信开源项目，替代SP
    implementation 'com.tencent:mmkv-static:1.2.10'

    //屏幕适配
    implementation 'me.jessyan:autosize:1.2.1'

    //防崩溃
    implementation 'cat.ereza:customactivityoncrash:2.3.0'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    //底部导航栏
    implementation 'com.github.Vincent7Wong:EasyNavigation:2.0.5'

    //工具类
    implementation 'com.blankj:utilcodex:1.30.6'

    kapt "com.ljx.rxhttp:rxhttp-compiler:2.5.5"
}